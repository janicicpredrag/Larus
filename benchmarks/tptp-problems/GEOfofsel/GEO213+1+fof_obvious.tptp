%------------------------------------------------------------------------------
% File     : GEO213+1 : TPTP v8.2.0. Released v3.3.0.
% Domain   : Geometry (Constructive)
% Problem  : Corollary to non-orthogonal lines and a third line
% Version  : [vPl95] axioms : Especial.
% English  : If line L is not orthogonal to line M, then a third line N
%            is distinct from L or not orthogonal to M.

% Refs     : [vPl95] von Plato (1995), The Axioms of Constructive Geometry
%          : [ROK06] Raths et al. (2006), The ILTP Problem Library for Intu
% Source   : [ILTP]
% Names    : Corollary 8.4 [vPl95]

% Status   : Theorem
% Rating   : 0.00 v6.1.0, 0.04 v6.0.0, 0.25 v5.5.0, 0.08 v5.4.0, 0.09 v5.3.0, 0.17 v5.2.0, 0.14 v5.0.0, 0.05 v4.1.0, 0.06 v4.0.1, 0.05 v3.7.0, 0.00 v3.3.0
% Syntax   : Number of formulae    :   23 (   7 unt;   0 def)
%            Number of atoms       :   59 (   0 equ)
%            Maximal formula atoms :    6 (   2 avg)
%            Number of connectives :   47 (  11   ~;  17   |;   4   &)
%                                         (   0 <=>;  15  =>;   0  <=;   0 <~>)
%            Maximal formula depth :    9 (   5 avg)
%            Maximal term depth    :    2 (   1 avg)
%            Number of predicates  :    5 (   5 usr;   0 prp; 2-2 aty)
%            Number of functors    :    4 (   4 usr;   0 con; 2-2 aty)
%            Number of variables   :   56 (  56   !;   0   ?)
% SPC      : FOF_THM_RFO_NEQ

% Comments : Definitions unfolded, hence Especial.
%          : tptp2X 
%------------------------------------------------------------------------------
fof(apart1,axiom,(
    ! [X] : 
      ~ distinct_points(X,X) )).

fof(apart2,axiom,(
    ! [X] : 
      ~ distinct_lines(X,X) )).

fof(apart3,axiom,(
    ! [X] : 
      ~ convergent_lines(X,X) )).

fof(apart4,axiom,(
    ! [X,Y,Z] : 
      ( distinct_points(X,Y)
     => ( distinct_points(X,Z)
        | distinct_points(Y,Z) ) ) )).

fof(apart5,axiom,(
    ! [X,Y,Z] : 
      ( distinct_lines(X,Y)
     => ( distinct_lines(X,Z)
        | distinct_lines(Y,Z) ) ) )).

fof(ax6,axiom,(
    ! [X,Y,Z] : 
      ( convergent_lines(X,Y)
     => ( convergent_lines(X,Z)
        | convergent_lines(Y,Z) ) ) )).

fof(ci1,axiom,(
    ! [X,Y] : 
      ( distinct_points(X,Y)
     => ~ apart_point_and_line(X,line_connecting(X,Y)) ) )).

fof(ci2,axiom,(
    ! [X,Y] : 
      ( distinct_points(X,Y)
     => ~ apart_point_and_line(Y,line_connecting(X,Y)) ) )).

fof(ci3,axiom,(
    ! [X,Y] : 
      ( convergent_lines(X,Y)
     => ~ apart_point_and_line(intersection_point(X,Y),X) ) )).

fof(ci4,axiom,(
    ! [X,Y] : 
      ( convergent_lines(X,Y)
     => ~ apart_point_and_line(intersection_point(X,Y),Y) ) )).

fof(cu1,axiom,
    ! [X,Y,U,V] :
      ( ( distinct_points(X,Y)
        & distinct_lines(U,V)
& ~apart_point_and_line(X,U)
& ~apart_point_and_line(X,V)
& ~apart_point_and_line(Y,U)
& ~apart_point_and_line(Y,V)
        )
     => $false ) ).

fof(ceq1,axiom,(
    ! [X,Y,Z] : 
      ( apart_point_and_line(X,Y)
     => ( distinct_points(X,Z)
        | apart_point_and_line(Z,Y) ) ) )).

fof(ceq2,axiom,(
    ! [X,Y,Z] : 
      ( apart_point_and_line(X,Y)
     => ( distinct_lines(Y,Z)
        | apart_point_and_line(X,Z) ) ) )).

fof(ceq3,axiom,(
    ! [X,Y,Z] : 
      ( convergent_lines(X,Y)
     => ( distinct_lines(Y,Z)
        | convergent_lines(X,Z) ) ) )).

fof(cp1,axiom,(
    ! [X,Y] : 
      ~ convergent_lines(parallel_through_point(Y,X),Y) )).

fof(cp2,axiom,(
    ! [X,Y] : 
      ~ apart_point_and_line(X,parallel_through_point(Y,X)) )).

fof(cup1,axiom,(
    ! [X,Y,Z] : 
      ( distinct_lines(Y,Z)
     => ( apart_point_and_line(X,Y)
        | apart_point_and_line(X,Z)
        | convergent_lines(Y,Z) ) ) )).

fof(occu1,axiom,(
    ! [L,M] : 
      ( convergent_lines(L,M)
      | unorthogonal_lines(L,M) ) )).

fof(oac1,axiom,(
    ! [L,M,N] : 
      ( ( convergent_lines(L,M)
        & unorthogonal_lines(L,M) )
     => ( ( convergent_lines(L,N)
          & unorthogonal_lines(L,N) )
        | ( convergent_lines(M,N)
          & unorthogonal_lines(M,N) ) ) ) )).

fof(ooc1,axiom,(
    ! [A,L] : 
      ~ unorthogonal_lines(orthogonal_through_point(L,A),L) )).

fof(ooc2,axiom,(
    ! [A,L] : 
      ~ apart_point_and_line(A,orthogonal_through_point(L,A)) )).

fof(ouo1,axiom,(
    ! [A,L,M,N] : 
      ( distinct_lines(L,M)
     => ( apart_point_and_line(A,L)
        | apart_point_and_line(A,M)
        | unorthogonal_lines(L,N)
        | unorthogonal_lines(M,N) ) ) )).

fof(con,conjecture,(
    ! [L,M,N] : 
      ( unorthogonal_lines(L,M)
     => ( distinct_lines(L,N)
        | unorthogonal_lines(M,N) ) ) )).
%------------------------------------------------------------------------------
